plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation "com.twilio:guardrail_2.12:0.53.1"
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    testImplementation 'cglib:cglib-nodep:3.2.10'
}

group = 'com.twilio.guardrail.plugin'
version = '1.0.0-SNAPSHOT'

gradlePlugin {
    plugins {
        guardrailPlugin {
            id = 'com.twilio.guardrail.plugin'
            implementationClass = 'com.twilio.guardrail.GuardrailGradlePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/head-thrash/guardrail-gradle-plugin'
    vcsUrl = 'https://github.com/head-thrash/guardrail-gradle-plugin'
    description = 'Twilio Guardrail Generator Plugin'
    tags = ['twilio', 'api', 'generator']

    plugins {
        guardrailPlugin {
            id = 'com.twilio.guardrail.plugin'
            displayName = 'Twilio Guardrail Generator Plugin'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}
